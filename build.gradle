import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
        classpath 'io.freefair.gradle:lombok-plugin:8.6'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '8.6'
}

group 'me.realized'
version '3.2.8'

compileJava.options.encoding = 'UTF-8'

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version, SHADED_GROUP: group])
    }
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/public'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'clip-repo'
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.io/repository/maven-public/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }

    flatDir {
        dirs "$rootDir/libs/"
    }
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.32'
    implementation 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
    implementation (name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    implementation name: 'Vault-1.6.7'
    implementation 'me.clip:placeholderapi:2.11.5'

    // shaded dependencies
    implementation 'redis.clients:jedis:5.1.2'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'org.apache.commons:commons-pool2:2.12.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.slf4j:slf4j-nop:2.0.13'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

clean.doFirst {
    delete "$rootDir/out/"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    destinationDirectory = file("$rootDir/out/")
    archiveBaseName = 'TokenManager'
    archiveClassifier = null

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
}

build.dependsOn(shadowJar)